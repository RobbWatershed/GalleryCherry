apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'org.sonarqube'

// apply signing configuration if config file exists
if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 28
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "me.devsaki.cherry"
        minSdkVersion 19
        //noinspection ExpiringTargetSdkVersion
        targetSdkVersion 28
        versionCode 130 // is updated automatically by BitRise; only used when building locally
        versionName '1.4.1-dev'

        def includeObjectBoxBrowser = System.getenv("INCLUDE_OBJECTBOX_BROWSER")?:"false"

        buildConfigField "boolean", "INCLUDE_OBJECTBOX_BROWSER", includeObjectBoxBrowser

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    buildTypes {
        debug {
            minifyEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/RobbWatershed/GalleryCherry/dev/app/"'

            proguardFile 'proguard-rules-debug.pro'
        }
        release {
            debuggable false
            minifyEnabled true

            buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/RobbWatershed/GalleryCherry/master/app/"'

            proguardFile 'proguard-rules-release.pro'
            testProguardFile 'proguard-rules-test.pro'

            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    def includeObjectBoxBrowser = System.getenv("INCLUDE_OBJECTBOX_BROWSER") ?: "false"

    // Compile any included libraries
    implementation fileTree(include: ['*jar'], dir: 'libs')
    implementation fileTree(include: ['*aar'], dir: 'libs')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.50"

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.1'

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Support libraries
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    //implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha08'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.1'
    implementation "androidx.fragment:fragment-ktx:1.1.0"

    // Image loader: github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt('com.github.bumptech.glide:compiler:4.11.0')
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'

    // Intro screens or slides: github.com/apl-devs/AppIntro
    implementation 'com.github.paolorotolo:appintro:v5.1.0'

    // Java serialization/deserialization (Java Objects into JSON and back): https://github.com/square/moshi
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.8.0'

    // Timber (Extensible Logger): github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Download app updates (without user prompts): github.com/smanikandan14/ThinDownloadManager
    implementation 'com.mani:ThinDownloadManager:1.3.0'

    // HTTP & HTTP/2 client: github.com/square/okhttp
    // upgrading to okhttp4 or > 3.12.5 will drop support for android 4.4 (kitkat)
    implementation 'com.squareup.okhttp3:okhttp:3.12.5'

    // For downloading images : github.com/google/volley
    implementation 'com.android.volley:volley:1.1.1' // <-- if this evolves, please examine RequestQueueManager.getRequestQueue for evolutions

    // EventBus: github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // JSOUP HTML parser: github.com/jhy/jsoup
    implementation 'org.jsoup:jsoup:1.11.3'

    // Lightweight Stream : github.com/aNNiMON/Lightweight-Stream-API
    implementation 'com.annimon:stream:1.2.1'

    // LeakCanary: github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'

    // Specific UI layout for tag mosaic : github.com/google/flexbox-layout
    implementation 'com.google.android:flexbox:2.0.0'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.9'

    // Retrofit-ready-ready HTML parser with CSS selectors : https://github.com/DroidsOnRoids/jspoon
    implementation 'pl.droidsonroids:jspoon:1.3.2'
    implementation 'pl.droidsonroids.retrofit2:converter-jspoon:1.3.2'

    //    https://github.com/square/retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'

    //    https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // For ViewModel to work
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    kapt('androidx.lifecycle:lifecycle-common-java8:2.1.0')

    // ObjectBox browser dependencies must be set before applying ObjectBox plugin so it does not add objectbox-android
    // (would result in two conflicting versions, e.g. "Duplicate files copied in APK lib/armeabi-v7a/libobjectbox.so").
    if (includeObjectBoxBrowser.toBoolean()) {
        debugImplementation "io.objectbox:objectbox-android-objectbrowser:2.5.0"
        releaseImplementation "io.objectbox:objectbox-android:2.5.0"
    }

    // https://github.com/davemorrissey/subsampling-scale-image-view
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

    // Cleaner date manipulation
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    // https://github.com/mikepenz/FastAdapter
    implementation "com.mikepenz:fastadapter:5.0.0-a03" // needs the `expandable`, `drag` and `scroll` extension.
    implementation "com.mikepenz:fastadapter-extensions-drag:5.0.0-a03" // drag support
    implementation "com.mikepenz:fastadapter-extensions-expandable:5.0.0-a03"
    implementation "com.mikepenz:fastadapter-extensions-scroll:5.0.0-a03" // scroll helpers
    implementation "com.mikepenz:fastadapter-extensions-paged:5.0.0-a03"
    implementation "com.mikepenz:fastadapter-extensions-ui:5.0.0-a03" // needs the `expandable`, `drag` and `scroll` extension.
    implementation "com.mikepenz:fastadapter-extensions-utils:5.0.0-a03" // needs the `expandable`, `drag` and `scroll` extension.
    implementation "com.mikepenz:materialize:1.2.1"

    // ProgressDialog alternative - https://github.com/Livin21/MissMe
    implementation 'com.lmntrx.android.library.livin.missme:missme:0.1.5'
}

sonarqube {
    properties {
        property "sonar.projectKey", "RobbWatershed_GalleryCherry"
        property "sonar.organization", "robbwatershed-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
        property "sonar.sources", "src/main/"
        property "sonar.login", "7bd8f2c4d82122f3261f244bd86403bedceea072"
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.objectbox' // apply last